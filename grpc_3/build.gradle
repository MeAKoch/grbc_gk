plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.5'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.3'
}

repositories {
    mavenCentral()
}

def grpcVersion = '1.76.0'
def protobufVersion = '3.25.8'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    // Needed on Java 9+
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    annotationProcessor "jakarta.annotation:jakarta.annotation-api:2.1.1"

    testImplementation 'junit:junit:4.13.2'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir 'app/src/proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc',
                    'build/generated/source/proto/main/java'
        }
    }
}


tasks.register('runServer', JavaExec) {
    group = 'application'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'HelloWorldServer'
}
